/** ChIP-Seq
 *
 * Tools and versions:
 *
 * sra-tools   2.6.2
 * FastQC      0.11.5
 * Bowtie      1.1.2
 * MACS        1.4.2-1
 *
 */

%% =========================================================
%% TASK DEFINITIONS
%% =========================================================

% sra-tools
deftask fastq-dump( fastq( File ) : sra( File ) )in bash *{
  fastq=$sra.fastq
  fastq-dump -Z $sra > $fastq
}*

% FastQC
deftask fastqc( zip( File ) : fq( File ) )in bash *{
  fastqc -f fastq --noextract -o ./ $fq
  zip=`ls *.zip`
}*

deftask bowtie-build( idx( File ) : fa( File ) )in bash *{
  bowtie-build $fa btidx
  idx=idx.tar
  tar cf $idx btidx.* --remove-files
}*

deftask bowtie-align( sam( File ) : idx( File ) fq( File ) )in bash *{
  tar xf $idx
  sam=$fq.sam
  bowtie btidx -q $fq -v 2 -m 1 -3 1 -S > $sam
}*

deftask macs(
    peaks( File ) summits( File ) <xls( File )>
    bedgraph_tag( File ) bedgraph_ctl( File )
  : sam_tag( File ) sam_ctl( File ) )in bash *{

  macs14 -t $sam_tag -c $sam_ctl --format SAM --gsize 4639675 --name "macs14" \
    --bw 400 --keep-dup 1 --bdg --single-profile --diag

  peaks=macs14_peaks.bed
  summits=macs14_summits.bed
  xls=(macs14_diag.xls macs14_negative_peaks.xls)
  bedgraph_tag=macs14_MACS_bedGraph/treat/macs14_treat_afterfiting_all.bdg.gz
  bedgraph_ctl=macs14_MACS_bedGraph/control/macs14_control_afterfiting_all.bdg.gz
}*

deftask samtools-sort( sorted_bam( File ) : sam( File ) )in bash *{
  sorted_bam=sorted.bam
  samtools view -bS $sam | samtools sort - sorted
}*

deftask samtools-rmdup( dedup_bam( File ) : bam( File ) )in bash *{
  dedup_bam=dedup.bam
  samtools rmdup -s $bam $dedup_bam
}*

deftask samtools-index( bai( File ) : bam( File ) )in bash *{
  bai=$bam.bai
  samtools index $bam $bai
}*

deftask bamcoverage( bedgraph( File ) : bam( File ) bai( File ) )in bash *{
  bedgraph=$bam.bedgraph
  bamCoverage --bam $bam --bamIndex $bai --outFileName $bedgraph \
    --fragmentLength 200 --normalizeTo1x 4639675
}*


%% =========================================================
%% INPUT DATA
%% =========================================================

sra_tag = "sra/SRR576933.sra";
sra_ctl = "sra/SRR576938.sra";
fa      = "ref/Escherichia_coli_K_12_MG1655.fasta";


%% =========================================================
%% WORKFLOW DEFINITION
%% =========================================================

% extract fastq data
fq_tag = fastq-dump( sra: sra_tag );
fq_ctl = fastq-dump( sra: sra_ctl );

% quality control
qc = fastqc( fq: fq_tag fq_ctl );

% read mapping
idx     = bowtie-build( fa: fa );
sam_tag = bowtie-align( idx: idx, fq: fq_tag );
sam_ctl = bowtie-align( idx: idx, fq: fq_ctl );

% peak calling with macs
peaks summits xls bedgraph_macs_tag bedgraph_macs_ctl = macs(
  sam_tag: sam_tag, sam_ctl: sam_ctl );

% visualization with deeptools
sorted_bam_tag         = samtools-sort( sam: sam_tag );
dedup_sorted_bam_tag   = samtools-dedup( bam: sorted_bam_tag );
dedup_sorted_bai_tag   = samtools-index( bam: dedup_sorted_bam_tag );
bedgraph_deeptools_tag = bamcoverage( bam: dedup_sorted_bam_tag, bai: dedup_sorted_bai_tag );


%% =========================================================
%% QUERY
%% =========================================================

qc peaks
bedgraph_macs_tag bedgraph_macs_ctl
bedgraph_deeptools_tag bedgraph_deeptools_ctl;


